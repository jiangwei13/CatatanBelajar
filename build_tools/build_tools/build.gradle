plugins {
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm'
}

//java {
//    sourceCompatibility = JavaVersion.VERSION_1_8
//    targetCompatibility = JavaVersion.VERSION_1_8
//}
jar {
    archiveBaseName = "dynamic_builder"
    archiveVersion = "1.0"
    archiveFileName.set("dynamic_builder.jar")
    manifest {
        attributes 'Main-Class': 'tools.DynamicBuilderKt'
    }
    destinationDirectory = new File("$rootProject.projectDir/build_tools/jiagu/")
    //添加将引用的jar的源码打入最终的jar
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }

    from fileTree(dir: 'src/main', includes: ['assets/**'])

    //排除引用的jar中的签名信息
    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.MF', 'META-INF/*.txt', "META-INF/versions/**"

    manifest {
        attributes("Implementation-Title": archiveBaseName,
                "Implementation-Version": archiveVersion,
                "Build-Time": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "Build-Number": System.env.BUILD_NUMBER ? System.env.BUILD_NUMBER : "-1",
        )
    }
    from(project.parent.projectDir) {
        include 'NOTICE.txt'
        include 'LICENSE.txt'
        into('META-INF')
    }
}

//添加源码中引入的非代码文件，例如资源等
sourceSets.main.resources {
    srcDirs = [
            "src/main/java",
    ];
    include "**/*.*"
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.+'
    implementation("com.github.javaparser:javaparser-core:3.23.0")
}